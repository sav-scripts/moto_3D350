<script type="x-shader/x-vertex" id="vertex">

    #define M_PI 3.1415926535897932384626433832795

    const float PIx2 = M_PI *2.0;

    const vec3 axisX = vec3(1.0, .0, .0);
    const vec3 axisY = vec3(.0, 1.0, .0);
    const vec3 axisZ = vec3(.0, .0, 1.0);

    uniform float time;
    uniform float floatScale;
    uniform vec3 projectedMouse;
    uniform float innerAlpha;
    uniform float dotScale;
    uniform float lastMouseMoveTime;

    uniform float introProgress;
    uniform float globalTurnSpeed;

    uniform float mapWidth;
    uniform float mapHeight;
    uniform float opacity;

    attribute float randomSeed;
    attribute float size;
    attribute float alpha;
    attribute vec3 floatCenter;
    attribute float isEdge;
    attribute float isSP;

    varying float vOpacity;
    varying float vAlpha;
    varying float vIsEdge;


    float quadOut(float t, float b, float c, float d)
    {
        t /= d;
        return -c * t*(t-2.0) + b;
    }

    float quadIn(float t, float b, float c, float d)
    {
        t /= d;
        return c*t*t + b;
    }

    float cubicOut(float t, float b, float c, float d)
    {
        t /= d;
        t--;
        return c*(t*t*t + 1.0) + b;
    }

    float cubicIn(float t, float b, float c, float d)
    {
        t /= d;
        return c*t*t*t + b;
    }


    float cubicInOut(float t, float b, float c, float d)
    {
        t /= d/2.0;
        if (t < 1.0) return c/2.0*t*t*t + b;
        t -= 2.0;
        return c/2.0*(t*t*t + 2.0) + b;
    }


    mat4 rotationMatrix(vec3 axis, float angle)
    {
        axis = normalize(axis);
        float s = sin(angle);
        float c = cos(angle);
        float oc = 1.0 - c;


        return mat4(oc * axis.x * axis.x + c,           oc * axis.x * axis.y - axis.z * s,  oc * axis.z * axis.x + axis.y * s,  0.0,
                    oc * axis.x * axis.y + axis.z * s,  oc * axis.y * axis.y + c,           oc * axis.y * axis.z - axis.x * s,  0.0,
                    oc * axis.z * axis.x - axis.y * s,  oc * axis.y * axis.z + axis.x * s,  oc * axis.z * axis.z + c,           0.0,
                    0.0,                                0.0,                                0.0,                                1.0);
    }

    vec3 applyRotation(vec3 target, vec3 axis, float angle)
    {
        mat4 matrix = rotationMatrix(axis, angle);
        return (vec4(target, 1.0) * matrix).xyz;
    }

    vec3 testCenter(vec3 vp, vec3 center, float threshold, float dTime)
    {
        float dist = distance(vp, center);
        float pushRange = smoothstep(threshold, .0, dist);

        if(dist <= threshold)
        {
            vec3 d = vp - projectedMouse;

            //d = normalize(d);
            //d = d * pushRange;

            float arc = dTime*.9 * pushRange;
            mat4 matrix = rotationMatrix(vec3(.0, .0, 1.0), arc);

            d = (vec4(d, 1.0)*matrix).xyz;

            vp.x = projectedMouse.x + d.x;
            vp.y = projectedMouse.y + d.y;
            //vp.z = quadIn(pushRange, .0, -10.0, 1.0);

            return vp;
        }
        else
        {
            return vec3(.0, .0, 4321.0);
        }

    }

    vec3 testCenter2(vec3 vp, vec3 floatCenter)
    {
        if(floatCenter.z > .0)
        {
            float threshold = floatCenter.z;
            vec3 center = vec3(floatCenter.xy, .0);

            float dist = distance(vp, center);

            if(dist <= threshold)
            {
                vec3 d = vp - center;

                vec3 dNormal = normalize(d);


                float f = smoothstep(.0, threshold, length(d));
                //float r = (quadIn(f, .0, 1.0, 1.0)*.5 + .5) * threshold;
                float r = quadIn(f, threshold*.9, threshold*.1, 1.0);

                d = dNormal * r;


                float pushRange = smoothstep(threshold, .0, dist);
                //float pushRange = smoothstep(threshold, threshold*.5, r);

                float arc = time*.25 * pushRange;
                mat4 matrix = rotationMatrix(axisZ, arc);

                d = (vec4(d, 1.0)*matrix).xyz;

                vp.x = center.x + d.x;
                vp.y = center.y + d.y;
                //vp.z = quadIn(pushRange, .0, -10.0, 1.0);
            }
        }



        return vp;
    }

    vec3 getGlobalPosition(float radius, float globalRotate)
    {
        float arcX = position.y / mapHeight * PIx2;

        float arcY = position.x / mapWidth * PIx2 + globalRotate;

        vec3 vp = vec3(.0 , .0, radius);

        vp = applyRotation(vp, axisY, arcY);
        vp = applyRotation(vp, vec3(cos(arcY), .0, -sin(arcY)), -arcX*.5);


        /* use this if not use matrixX
        vp.y = radius * sin(arcX*.5);
        float rate = cos(-arcX*.5);

        vp.z *= rate;
        vp.x *= rate;
        */



        return vp;

    }

    void main()
    {
        vAlpha = alpha;
        vIsEdge = isEdge;
        vOpacity = opacity;

        const vec3 zAxis = vec3(.0, .0, 1.0);

        vec3 vp, globalPosition;

        float pGlobalScaling = smoothstep(.6, .0, introProgress);

        float pGlobalToRandom = cubicOut(smoothstep(.5, .15, introProgress), .0, 1.0, 1.0);

        float pRandomToRandom = cubicOut(smoothstep(.7, .5, introProgress), .0, 1.0, 1.0);

        float pRandomToMap = cubicInOut(smoothstep(1.0, .5, introProgress), .0, 1.0, 1.0);





        float flip = mod(randomSeed, .1) > .05? 1.0: -1.0;
        float r = (sin(time*randomSeed) + 1.0) * floatScale;
        float arc2 = time*randomSeed * flip;
        vec3 offset = vec3(r * cos(arc2), r * sin(arc2), .0);

        if(isEdge == 1.0) offset *= .5;

        vp = position + offset;

        float dTime = time - lastMouseMoveTime;
        if(introProgress == 1.0 && dTime > .3 && !(projectedMouse.x == .0 && projectedMouse.y == .0 && projectedMouse.z == .0))
        {
            vec3 twistedVp = testCenter(position, projectedMouse, 50.0, dTime);
            if(twistedVp.z != 4321.0)
            {
                vp = twistedVp;
            }
        }


        vec3 randomPosition1 = position * 1.0;
        randomPosition1.z = randomSeed * 2.0 * 500.0 + 500.0;

        if(introProgress < 1.0)
        {
            float radius = 80.0;
            //radius = smoothstep(.0, 1.0, pGlobalScaling) * radius*.5 + radius*.5;

            float globalRotate = globalTurnSpeed * time;
            globalRotate += (1.0 - pGlobalScaling) * 15.0;

            globalPosition = getGlobalPosition(radius + position.z, globalRotate);

            vOpacity -= smoothstep(radius, -radius, globalPosition.z)*pGlobalToRandom * vOpacity;



            vec3 randomPosition = globalPosition * 100.0 * randomSeed + 100.0;


            if(pRandomToRandom > .0)
            {
                randomPosition1 = mix(randomPosition1, randomPosition, pRandomToRandom);
            }

            if(pGlobalToRandom > .0)
            {
                vp = mix(randomPosition1, globalPosition, pGlobalToRandom);
            }
            else if(pRandomToMap > .0)
            {
                vp = mix(vp, randomPosition1, pRandomToMap);
            }
        }


        vec4 mvPosition = modelViewMatrix * vec4( vp, 1.0 );

        //float scale = isEdge == 1.0? 1.0: dotScale;
        float scale = dotScale;
        gl_PointSize = 1000.0 * size * ( 1.0 / length( mvPosition.xyz ) ) * scale;

        gl_Position = projectionMatrix * mvPosition;




    }


</script>

<script type="x-shader/x-fragment" id="fragment">

    uniform vec3 dotColor;
    uniform sampler2D texture;
    uniform float innerAlpha;
    uniform float outerAlpha;

    varying float vAlpha;
    varying float vIsEdge;
    varying float vOpacity;

    void main()
    {

        float alpha = vAlpha * vOpacity;

        //(vIsEdge <= .5)? alpha *= innerAlpha: alpha *= outerAlpha;
        alpha *= innerAlpha;


        vec4 outColor = texture2D( texture, gl_PointCoord );


        gl_FragColor = outColor * vec4( dotColor, alpha );

        /*
        float depth = gl_FragCoord.z / gl_FragCoord.w;
        float fogFactor = smoothstep( 1000.0, 0.0, depth );
        gl_FragColor.w *= fogFactor;
        */

    }

</script>